plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.9'
}

group = 'me.superchirok1.playeraccesslist'
version = '3.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    implementation project(':common')
    implementation "net.kyori:adventure-text-minimessage:4.25.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"
    implementation "net.kyori:adventure-api:4.25.0"
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.processResources {
    filesMatching("plugin.yml") {
        expand(project.properties)
    }
}

shadowJar {
    archiveBaseName.set("PAL")
    archiveVersion.set(project.version)
    archiveClassifier.set("PAPER")

    mergeServiceFiles()
    configurations = [project.configurations.runtimeClasspath]

    relocate "net.kyori.adventure", "me.superchirok1.playeraccesslist.adventure"

    from(project(':common').sourceSets.main.output)
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}
